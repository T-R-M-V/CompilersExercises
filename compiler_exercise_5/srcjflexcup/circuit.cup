// CUP specification for a simple expression evaluator (no actions)

import java_cup.runtime.*;
import java.util.List;
import java.util.ArrayList;

import org.example.LexerInfo;

import org.example.tree.statements.*;
import org.example.tree.expr.*;
import org.example.tree.*;

import org.example.Type;

/* Terminals (tokens returned by the scanner). */
terminal    LexerInfo   PROGRAM, BEGIN, END;
terminal    LexerInfo   SEMI, COLON;
terminal    LexerInfo   INT, BOOL, DOUBLE, STRING, CHAR;
terminal    LexerInfo   ID;
terminal    LexerInfo   COMMA;
terminal    LexerInfo   DEF, RETURN, REF;
terminal    LexerInfo   LPAR, RPAR, LBRAC, RBRAC;
terminal    LexerInfo   IN, OUT, OUTNL;
terminal    LexerInfo   PLUS, MINUS, TIMES, DIV;
terminal    LexerInfo   INT_CONST, DOUBLE_CONST, STRING_CONST, CHAR_CONST;
terminal    LexerInfo   TRUE, FALSE;
terminal    LexerInfo   ASSIGN, ASSIGNDECL;
terminal    LexerInfo   IF, THEN, WHILE, DO, ELSE;
terminal    LexerInfo   GT, GE, LT, LE, EQ, NE;
terminal    LexerInfo   NOT, AND, OR;
terminal    LexerInfo   PIPE;

/* Non terminals */
non terminal    ProgramOpNode               Programma;
non terminal    TempNode.TempAggregateNode  Decls;
non terminal    TempNode.TempDeclsNode      VarDecls;
non terminal    VarDeclOpNode               VarDecl;
non terminal    TempNode.TempAggregateNode  VarsOptInit;
non terminal    TypeOrConstantNode          TypeOrConstant;
non terminal    TypeNode                    Type;
non terminal    ConstantNode                Constant;
non terminal    DefDeclOpNode               DefDecl;
non terminal    TempNode.TempAggregateNode  ParDecls;
non terminal    ParDeclOpNode               ParDecl;
non terminal    TempNode.TempAggregateNode  PVars;
non terminal    PVarOpNode                  PVar;
non terminal    TypeNode                    OptType;
non terminal    BodyOpNode                  Body;
non terminal    TempNode.TempAggregateNode  Statements;
non terminal    StatOpNode                  Stat;
non terminal    TempNode.TempAggregateNode  Vars;
non terminal    TempNode.TempAggregateNode  Exprs;
non terminal    ExprOpNode                  Expr;
non terminal    CallOpNode                  FunCall;
non terminal    BinaryOpNode.Type           ArithOp;
non terminal    BinaryOpNode.Type           BoolOp;
non terminal    BinaryOpNode.Type           RelOp;

/* Precedences */
precedence left OR;
precedence left AND;
precedence nonassoc NOT;
precedence nonassoc EQ, NE, LT, LE, GT, GE; /* We don't excatly know why nonassoc works, there is a conflict */
precedence left PLUS, MINUS;
precedence left TIMES, DIV;
/* precedence nonassoc LPAR, RPAR; */





/* Grammar */
Programma ::= PROGRAM Decls:decls BEGIN:bTest VarDecls:varDecls Statements:statements END
            {:
            BeginEndOpNode beginEndOpNode = new BeginEndOpNode(varDecls, (List)statements.children);
            RESULT = new ProgramOpNode((List)decls.children, beginEndOpNode);
            :};

Decls ::= VarDecl:varDecl Decls:decls {: RESULT = new TempNode.TempAggregateNode(decls, varDecl, true); :}
      | DefDecl:defDecl Decls:decls {: RESULT = new TempNode.TempAggregateNode(decls, defDecl, true); :}
      | /*eps*/ {: RESULT = new TempNode.TempAggregateNode(); :};

VarDecls ::= VarDecls:varDecls VarDecl:varDecl {: varDecls.addVarDeclOpNode(varDecl); RESULT = varDecls; :}
         | /*eps*/ {: RESULT = new TempNode.TempDeclsNode(); :};

VarDecl ::= VarsOptInit:varsOptInit COLON TypeOrConstant:typeOrConstant SEMI {: RESULT = new VarDeclOpNode((List)varsOptInit.children, typeOrConstant); :};

VarsOptInit ::= ID:identifier PIPE VarsOptInit:varsOptInit {: RESULT = new TempNode.TempAggregateNode(varsOptInit, new VarOptInitOpNode(new IdentifierNode((String)identifier.value)), true); :}
	   | ID:identifier ASSIGNDECL Expr:expr PIPE VarsOptInit:varsOptInit {: RESULT = new TempNode.TempAggregateNode(varsOptInit, new VarOptInitOpNode(new IdentifierNode((String)identifier.value), expr), true); :}
	   | ID:identifier {: RESULT = new TempNode.TempAggregateNode(new VarOptInitOpNode(new IdentifierNode((String)identifier.value))); :}
	   | ID:identifier ASSIGNDECL Expr:expr {: RESULT = new TempNode.TempAggregateNode(new VarOptInitOpNode(new IdentifierNode((String)identifier.value), expr)); :} ;

TypeOrConstant ::= Type:type {: RESULT = new TypeOrConstantNode(type); :}
	       | Constant:constant {: RESULT = new TypeOrConstantNode(constant); :} ;

Type ::= INT {: RESULT = new TypeNode(Type.Integer); :}
           |  BOOL {: RESULT = new TypeNode(Type.Boolean); :}
           |  DOUBLE {: RESULT = new TypeNode(Type.Double); :}
           |  STRING {: RESULT = new TypeNode(Type.String); :}
           |  CHAR {: RESULT = new TypeNode(Type.Char); :} ;

Constant ::= TRUE {: RESULT = new ConstantNode(Type.Boolean, "true"); :}
          | FALSE {: RESULT = new ConstantNode(Type.Boolean, "false"); :}
          | INT_CONST:int_const {: RESULT = new ConstantNode(Type.Integer, (String)int_const.value); :}
          | DOUBLE_CONST:double_const {: RESULT = new ConstantNode(Type.Double, (String)double_const.value); :}
          | CHAR_CONST:char_const {: RESULT = new ConstantNode(Type.Char, (String)char_const.value); :}
          | STRING_CONST:string_const {: RESULT = new ConstantNode(Type.String, (String)string_const.value); :};

DefDecl ::= DEF ID:identifier LPAR ParDecls:parDecls RPAR OptType:optType Body:body
        {: RESULT = new DefDeclOpNode(new IdentifierNode((String)identifier.value), (List)parDecls.children, optType, body); :}

        | DEF ID:identifier LPAR  RPAR OptType:optType Body:body
        {: RESULT = new DefDeclOpNode(new IdentifierNode((String)identifier.value), new ArrayList<>(), optType, body); :};

ParDecls  ::= ParDecl:parDecl SEMI ParDecls:parDecls {: RESULT = new TempNode.TempAggregateNode(parDecls, parDecl, true); :}
          | ParDecl:parDecl {: RESULT = new TempNode.TempAggregateNode(parDecl); :};

ParDecl ::= PVars:pVars COLON Type:type {: RESULT = new ParDeclOpNode((List)pVars.children, type); :};

PVars ::= PVar:pVar COMMA PVars:pVars {: RESULT = new TempNode.TempAggregateNode(pVars, pVar, true); :}
      | PVar:pVar {: RESULT = new TempNode.TempAggregateNode(pVar); :};

PVar ::= ID:identifier {: RESULT = new PVarOpNode(new IdentifierNode((String)identifier.value)); :}
    | REF ID:identifier {: RESULT = new PVarOpNode(new IdentifierNode((String)identifier.value), true); :};


OptType ::= COLON Type:type {: RESULT = type; :}
         | /*eps*/ {: RESULT = new TypeNode(Type.Void); :} ;

Body ::= LBRAC VarDecls:varDecls Statements:statements RBRAC {: RESULT = new BodyOpNode((List)varDecls.varDeclOpNodes, (List)statements.children); :};

Statements ::= Stat:stat Statements:statements {: RESULT = new TempNode.TempAggregateNode(statements, stat, true); :}
	   | /*eps*/ {: RESULT = new TempNode.TempAggregateNode(); :};


Stat ::=  Vars:vars IN SEMI {: RESULT = new ReadOpNode((List)vars.children); :}
        | Exprs:exprs OUT SEMI {: RESULT = new WriteOpNode((List)exprs.children, false); :}
        | Exprs:exprs OUTNL SEMI {: RESULT = new WriteOpNode((List)exprs.children, true); :}
	| Vars:vars ASSIGN Exprs:exprs SEMI {: RESULT = new AssignOpNode((List)vars.children, (List)exprs.children); :}
	| FunCall:funcall SEMI {: RESULT = funcall; :}
        | IF LPAR Expr:expr RPAR THEN Body:body_1 ELSE Body:body_2 {: RESULT = new IfThenElseOpNode(expr, body_1, body_2); :}
        | IF LPAR Expr:expr RPAR THEN Body:body {: RESULT = new IfThenOpNode(expr, body); :}
        | WHILE LPAR Expr:expr RPAR DO Body:body {: RESULT = new WhileOpNode(expr, body); :}
	| RETURN Expr:expr SEMI {: RESULT = new ReturnOpNode(expr); :};


Vars   ::= ID:identifier PIPE Vars:vars {: RESULT = new TempNode.TempAggregateNode(vars, new IdentifierNode((String)identifier.value), true); :}
        | ID:identifier {: RESULT = new TempNode.TempAggregateNode(new IdentifierNode((String)identifier.value)); :};

Exprs ::=  Expr:expr COMMA  Exprs:exprs {: RESULT = new TempNode.TempAggregateNode(exprs, expr, true); :}
	| Expr:expr {: RESULT = new TempNode.TempAggregateNode(expr); :};


FunCall ::= ID:identifier LPAR Exprs:exprs RPAR {: RESULT = new CallOpNode(new IdentifierNode((String)identifier.value), (List)exprs.children); :}
        | ID:identifier LPAR RPAR {: RESULT = new CallOpNode(new IdentifierNode((String)identifier.value)); :};


Expr   ::= Expr:expr_1 ArithOp:op Expr:expr_2 {: RESULT = new BinaryOpNode(op, expr_1, expr_2); :}
		| Expr:expr_1 BoolOp:op Expr:expr_2 {: RESULT = new BinaryOpNode(op, expr_1, expr_2); :}
	        | Expr:expr_1 RelOp:op Expr:expr_2 {: RESULT = new BinaryOpNode(op, expr_1, expr_2); :}
		| LPAR Expr:expr RPAR {: RESULT = expr; :}
	        | MINUS Expr:expr {: RESULT = new UnaryOpNode(UnaryOpNode.Type.Uminus, expr); :}
	        | NOT Expr:expr {: RESULT = new UnaryOpNode(UnaryOpNode.Type.Not, expr); :}
	        | ID:identifier {: RESULT = new ExprValueNode(new IdentifierNode((String)identifier.value)); :}
                | FunCall:funcall {: RESULT = new ExprValueNode(funcall); :}
		| Constant:constant {: RESULT = new ExprValueNode(constant); :}
		;

ArithOp ::= PLUS {: RESULT = BinaryOpNode.Type.Plus; :}
        |  MINUS {: RESULT = BinaryOpNode.Type.Minus; :}
        | TIMES  {: RESULT = BinaryOpNode.Type.Times; :}
        |  DIV {: RESULT = BinaryOpNode.Type.Div; :}
        ;

BoolOp ::= AND {: RESULT = BinaryOpNode.Type.And; :}
        | OR {: RESULT = BinaryOpNode.Type.Or; :}
        ;

RelOp  ::=  GT {: RESULT = BinaryOpNode.Type.Gt; :}
        | GE {: RESULT = BinaryOpNode.Type.Ge; :}
        | LT {: RESULT = BinaryOpNode.Type.Lt; :}
        | LE {: RESULT = BinaryOpNode.Type.Le; :}
        | EQ {: RESULT = BinaryOpNode.Type.Eq; :}
        | NE {: RESULT = BinaryOpNode.Type.Ne; :}
        ;